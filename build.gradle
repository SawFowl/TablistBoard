plugins {
	id("java")
	id("java-library")
	id("eclipse")
	id("maven-publish")
	id("org.spongepowered.gradle.plugin") version "2.0.2"
}

group = "sawfowl.tablistboard"
version = "${major}.${minor}.${patch}-${api}-${suffix}"

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
	maven {
		name = "Sponge"
		url = "https://repo.spongepowered.org/repository/maven-public"
	}
	maven {
		name = "JitPack"
		url 'https://jitpack.io'
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
	implementation "org.spongepowered:spongeapi:${api}"
	implementation "com.github.SawFowl:LocaleAPI:${localeapi}"
	implementation "com.github.SawFowl:RegionGuard:${regionguard}"
	//implementation fileTree(dir: 'libs', include: '*.jar')
}

sponge {
	apiVersion("${api}")
	loader {
		name("java_plain")
		version("1.0")
	}
	license("GPL-3.0")
	plugin("tablistboard") {
		displayName(project.name.capitalize())
		version("${project.version}")
		entrypoint("${project.group}.${rootProject.name}")
		description("A plugin for creating scoreboard and tablist.")
		contributor("SawFowl") {
			description("Lead Developer")
		}
		dependency("spongeapi") {
			optional(false)
			loadOrder("after")
			version("${api}")
		}
		dependency("localeapi") {
			optional(false)
			loadOrder("after")
			version("${localeapi}")
		}
		dependency("regionguard") {
			optional(true)
			loadOrder("after")
			version("${regionguard}")
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

configurations {
	doc {
		transitive false
	}
}

javadoc {
	options.encoding = 'UTF-8'
	source configurations.doc.collect { zipTree(it) }
	include '**/*.java'
	options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            name = 'com.github.SawFowl'
            url = layout.buildDirectory.dir("repo")
        }
    }
}
